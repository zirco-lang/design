# Zirco programming language grammar
#
# https://github.com/zirco-lang

# Note: This document is a work-in-progress.

# Operator precedence:
# 1. Comma
# 2. Assignment
# 3. Ternary
# 4. Logical OR
# 5. Logical AND
# 6. Bitwise OR
# 7. Bitwise XOR
# 8. Bitwise AND
# 7. (In)Equality
# 8. Comparison
# 9. Bit Shift
# 10. Addition and Subtraction
# 11. Multiplication, Division, and Modulo
# 12. Unary (+ and -, logical and bitwise NOT, indirection, address-of)
# 13. Function call, array access, member access (and member access through pointer)
# 14. Primary
# WE HAVE NO ++ and -- OPERATORS YET.

program = <declaration-list>;

declaration-list = (<declaration>)*;
                 
# See https://en.wikipedia.org/wiki/Dangling_else#Avoiding_the_conflict_in_LR_parsers for why this is necessary.
# The ClosedStmt rule REQUIRES that an 'else' clause is used, meaning that it will attach to the inner 'if' statement.

statement = <open-stmt> | <closed-stmt>;

open-stmt = "if" "(" <expression> ")" <statement>
          | "if" "(" <expression> ")" <closed-stmt> "else" <open-stmt>
          | "while" "(" <expression> ")" <open-stmt>
          | "for" "(" <declaration> ";" <expression> ";" <expression> ")" <open-stmt>;

closed-stmt = <simple-stmt>
            | "if" "(" <expression> ")" <closed-stmt> "else" <closed-stmt>
            | "while" "(" <expression> ")" <closed-stmt>
            | "for" "(" <declaration> ";" <expression> ";" <expression> ")" <closed-stmt>;

simple-stmt = <expression> ";"
            | ";"
            | "{" <stmt-list>? "}"
            | <flow-stmt>
            | <declaration>;

declaration = <let-declaration> | <function-declaration>;

function-declaration = "fn" <IDENTIFIER> "(" <argument-declaration-list>? ")" ("->" <type>)? "{" <stmt-list>? "}";

argument-declaration-list = <argument-declaration> ("," <argument-declaration>)* ","?;

argument-declaration = <IDENTIFIER> (":" <type>)?;

let-declaration = "let" <single-declaration> ("," <single-declaration>)* ";";

single-declaration = <IDENTIFIER> (":" <type>)? ("=" <expression>)?;

# TODO: more type stuff
type = <IDENTIFIER>;

flow-stmt = "continue" ";"
          | "break" ";"
          | "return" <expression> ";"
          | "return" ";";

stmt-list = <statement>+;

expression = <expression> "," <assignment-expression>
           | <assignment-expression>;

assignment-expression = <unary-expression> <assignment-operator> <assignment-expression>
                      | <ternary-expression>;

assignment-operator = "=" | "+=" | "-=" | "*=" | "/=" | "%=" | "&=" | "|="
                    | "^=" | "<<=" | ">>=";

ternary-expression = <logical-expression> "?" <expression> ":" <ternary-expression>
                   | <logical-expression>;

logical-expression = <logical-or>;
logical-or = <logical-or> "||" <logical-and>
           | <logical-and>;

logical-and = <logical-and> "&&" <equality-expression>
            | <bitwise-expression>;

bitwise-expression = <bitwise-or>;
bitwise-or = <bitwise-or> "|" <bitwise-xor>
           | <bitwise-xor>;
bitwise-xor = <bitwise-xor> "^" <bitwise-and>
            | <bitwise-and>;
bitwise-and = <bitwise-and> "&" <equality-expression>
            | <equality-expression>;

equality-expression = <equality-expression> <equality-operator> <comparison-expression>
                    | <comparison-expression>;

equality-operator = "==" | "!=";

comparison-expression = <comparison-expression> <comparison-operator> <bit-shift-expression>
                      | <bit-shift-expression>;

comparison-operator = ">" | ">=" | "<" | "<=";

bit-shift-expression = <bit-shift-expression> <bit-shift-operator> <term-expression>
                     | <term-expression>;

bit-shift-operator = "<<" | ">>";

term-expression = <term-expression> "+" <factor-expression>
                | <term-expression> "-" <factor-expression>
                | <factor-expression>;

factor-expression = <factor-expression> "*" <unary-expression>
                  | <factor-expression> "/" <unary-expression>
                  | <factor-expression> "%" <unary-expression>
                  | <unary-expression>;

unary-expression = <unary-operator> <unary-expression>
                 | <postfix-expression>;

unary-operator = "-" | "!" | "~" | "&" | "*";

postfix-expression = <postfix-expression> "[" <expression> "]"
                   | <postfix-expression> "(" <argument-list>? ")"
                   | <postfix-expression> "." <IDENTIFIER>
                   | <postfix-expression> "->" <IDENTIFIER>
                   | <primary>;

argument-list = <argument-list> "," <assignment-expression>
              | <assignment-expression>;

primary = <IDENTIFIER> | <STRING> | <NUMBER> | "true" | "false" | "(" <expression> ")";
